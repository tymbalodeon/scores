#!/usr/bin/env zsh

get_new_score_name() {
    if [ "${1}" = "piano" ] \
        || [ "${1}" = "single" ]; then
        extension=".${2:e}"
    else
        extension="-${2:t}"
    fi
    file_name="${3}${extension}"
    printf "%s" "${4}/${file_name//-main}"
}

prepend_title_to_file_names() {
    filetypes=("changes" "lyrics" "melody" "structure")
    for filetype in "${filetypes[@]}"; do
        sed -i "" -e "s/${filetype}.ily/${1}-${filetype}.ily/g" "${2}"
    done
}

update_title_and_composer() {
    title="${(C)1}"
    composer="${(C)2}"
    title="${title//-/ }"
    composer="${composer//-/ }"
    perl -i -pe "s/Title/${title}/g" "${3}"
    perl -i -pe "s/Composer/${composer}/g" "${3}"
}

update_main_file() {
    if [[ "${TEMPLATE}" = *"lead" ]] \
        && [ "${LYRICS}" != "true" ]; then
        perl -i -pe 's/\\include "lyrics.ily"\n//g' "${1}"
        perl -i -pe 's/ \\addlyrics \\text//g' "${1}"
    fi
    prepend_title_to_file_names "${2}" "${1}"
    update_title_and_composer "${2}" "${3}" "${1}"
    mv "${1}" "${1//-main/}"
}

copy_template_files() {
    score_directory=./scores/"${2}"/"${3}"
    mkdir -p "${score_directory}"
    for template in ./templates/"${1}"*/*; do
        new_score="$(
            get_new_score_name \
                "${1}" "${template}" "${3}" "${score_directory}"
        )"
        if test -f "${new_score}"; then
            continue
        elif [[ "${template}" = *"lyrics.ily" ]] \
            && [ "${LYRICS}" != "true" ]; then
            continue
        fi
        cp "${template}" "${score_directory}"
        mv "${score_directory}/${template:t}" "${new_score}"
        if [[ "${template}" = *"main.ly" ]]; then
            update_main_file "${new_score}" "${3}" "${2}"
        fi
    done
}

IFS=" " read -r -A ARGS <<< "$@"
if ((${ARGS[(I)*form*]})); then
    TEMPLATE="form"
fi
if ((${ARGS[(I)*lead*]})); then
    if [ "${TEMPLATE}" = "form" ]; then
        TEMPLATE+="-lead"
    else
        TEMPLATE="lead"
    fi
elif ((${ARGS[(Ie)piano]})); then
    TEMPLATE="piano"
elif ((${ARGS[(Ie)single]})); then
    TEMPLATE="single"
fi
if [[ "${TEMPLATE}" = *"lead" ]] \
    && ((${ARGS[(I)*lyrics*]})); then
    LYRICS="true"
fi
copy_template_files "${TEMPLATE}" "${2}" "${3}"
if [ "${4}" = "--edit" ]; then
    just edit "${3}"
fi
