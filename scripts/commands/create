#!/usr/bin/env zsh

get_new_score_name () {
    if [ "${1}" = "piano" ]; then
        extension=".${2:e}"
    else
        extension="-${2:t}"
    fi
    file_name="${3}${extension}"
    printf "%s" "${4}/${file_name//-main}"
}

prepend_title_to_file_names() {
    filetypes=("changes" "lyrics" "melody" "structure")
    for filetype in "${filetypes[@]}"; do
        sed -i "" -e "s/${filetype}.ily/${1}-${filetype}.ily/g" "${2}"
    done
}

update_title_and_composer() {
    title="${(C)1}"
    composer="${(C)2}"
    perl -i -pe "s/Title/${title}/g" "${3}"
    perl -i -pe "s/Composer/${composer}/g" "${3}"
}

update_main_file() {
    if [ "${TEMPLATE}" = "lead" ] \
        || [ "${TEMPLATE}" = "chart" ] \
        && [ "${LYRICS}" != "true" ]; then
        perl -i -pe 's/\\include "lyrics.ily"\n//g' "${1}"
        perl -i -pe 's/ \\addlyrics \\words//g' "${1}"
    fi
    prepend_title_to_file_names "${2}" "${1}"
    update_title_and_composer "${2}" "${3}" "${1}"
    # mv "${1}" "${1//-main/}"
}

copy_template_files() {
    score_directory=./scores/"${2}"/"${3}"
    mkdir -p "${score_directory}"
    for template in ./templates/"${1}"*/*; do
        new_score="$(
            get_new_score_name \
                "${1}" "${template}" "${3}" "${score_directory}"
        )"
        if test -f "${new_score}"; then
            continue
        elif [[ "${template}" = *"lyrics.ily" ]] \
            && [ "${LYRICS}" != "true" ]; then
            continue
        fi
        cp "${template}" "${score_directory}"
        mv "${score_directory}/${template:t}" "${new_score}"
        if [[ "${template}" = *"main.ly" ]]; then
            update_main_file "${new_score}" "${3}" "${2}"
        fi
    done
}

IFS=" " read -r -A ARGS <<< "$@"
if ((${ARGS[(Ie)chart]})); then
    TEMPLATE="chart"
elif ((${ARGS[(Ie)form]})); then
    TEMPLATE="form"
elif ((${ARGS[(Ie)lead]})); then
    TEMPLATE="lead"
elif ((${ARGS[(Ie)piano]})); then
    TEMPLATE="piano"
fi
if [ "${TEMPLATE}" = "chart" ] \
    || [ "${TEMPLATE}" = "lead" ] \
     && ((${ARGS[(Ie)lyrics]})); then
    LYRICS="true"
fi
copy_template_files "${1}" "${2}" "${3}"
if [ "${4}" = "--edit" ]; then
    just edit "${3}"
fi
