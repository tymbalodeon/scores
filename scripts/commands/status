#!/usr/bin/env zsh

get_sorted_score_names() {
    score_directories=(
        **/.(Ne: \
            '[[ $REPLY != pdfs* ]] \
                && [[ $REPLY != templates* ]]  \
                && [[ $REPLY != scripts* ]]  \
                && test -z $REPLY/*(/N[1])' \
        ::h)
    )
    scores=()
    if [[ ! -z "$@" ]]; then
        for search_term in "$@"; do
            for score in "${score_directories[@]}"; do
                if [[ "${score}" = *"${search_term}"* ]]; then
                    scores+=("${score}")
                fi
            done
        done
    else
        for score in "${score_directories[@]}"; do
            scores+=("${score}")
        done
    fi
    IFS=$'\n' scores=($(sort <<<"${scores[*]}"))
    printf "%s" "${scores[*]}"
}

scores=($(get_sorted_score_names "$@"))
outdated_scores=($("${HELPERS}"/_get_outdated))
results="ARTIST;TITLE;PDF STATUS\n------;-----;----------\n"
for score in "${scores[@]}"; do
    file_name="${score:t}"
    score="${(C)${score//-/ }}"
    artist="${score:h:t}"
    results+="${artist};${score:t};"
    pdf_files=("${PDFS_DIRECTORY}"/*"${file_name}"*.pdf(N))
    if [ "${pdf_files}" = "" ]; then
        pdf_status="NO PDF"
    else
        pdf_status="up to date"
    fi
    for file in "${pdf_files[@]}"; do
        file_stem="${file:t:r}"
        score_name="${file_stem//-form/}"
        score_name="${score_name//-video/}"
        if ((${outdated_scores[(Ie)${score_name}]})); then
            pdf_status="OUTDATED"
        fi
    done
    results+="${pdf_status:-}\n"
done
echo "${results}" | column -t -s ";"
